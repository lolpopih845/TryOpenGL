#version 430 core

layout(location = 0) in vec3 pos;
layout(location = 1) in vec3 norm;
layout(location = 2) in vec2 tex;
layout(location = 5) in ivec4 boneIds;
layout(location = 6) in vec4 weights;

uniform mat4 model;
layout(std140) uniform Camera {
    mat4 uView;
    mat4 uProjection;
};

const int MAX_BONES = 100;
const int MAX_BONE_INFLUENCE = 4;
uniform mat4 finalBonesMatrices[MAX_BONES];

out vec2 TexCoords;

void main()
{
    vec4 totalPosition = vec4(0.0);
    float totalWeight = 0.0;
    for(int i = 0 ; i < MAX_BONE_INFLUENCE ; i++)
    {
        if (boneIds[i] < 0)
            continue;
        if(boneIds[i] >=MAX_BONES)
        {
            totalPosition = vec4(pos,1.0f);
            break;
        }
        totalPosition += (finalBonesMatrices[boneIds[i]] * vec4(pos, 1.0)) * weights[i];
        totalWeight += weights[i];
    }

    if (totalWeight < 0.0001)
        totalPosition = vec4(pos, 1.0);

    gl_Position = uProjection * uView * model * totalPosition;
    TexCoords = tex;
}